PREFIX dcat:    <http://www.w3.org/ns/dcat#>
PREFIX dc:      <http://purl.org/dc/terms/>
PREFIX prov:    <http://www.w3.org/ns/prov#>
PREFIX xsd:     <http://www.w3.org/2001/XMLSchema#>
PREFIX geof:    <http://www.opengis.net/def/function/geosparql/>
PREFIX geo:     <http://www.opengis.net/ont/geosparql#>
PREFIX schema:  <https://schema.org/>
SELECT DISTINCT (GROUP_CONCAT(DISTINCT ?_title; SEPARATOR=' ') AS ?title) (GROUP_CONCAT(DISTINCT ?_mediaType; SEPARATOR='|') AS ?mediaType) ?dataset ?description ?bbox (REPLACE(STR(?catalog), '\\?.*$', '') AS ?cleanCatalog)
WHERE {
    
    ?dataset a dcat:Dataset ;
        dc:title ?_title ;
        dc:description ?description .
    
    OPTIONAL {
        ?dataset dc:temporal [
            a dc:PeriodOfTime ;
            dcat:startDate ?startDate ;
            dcat:endDate ?endDate
        ] .
    }
    
    OPTIONAL {
        ?dataset dc:spatial [
            a dc:Location ;
            dcat:bbox ?bbox
        ] .
    }
    
    OPTIONAL {
        ?dataset schema:variableMeasured [
            a schema:PropertyValue ;
            schema:name ?parameterName
        ] .
    }   
    OPTIONAL {
        ?dataset prov:used ?used .
    }
    OPTIONAL {
        ?catalog a dcat:Catalog ;
            dcat:dataset ?dataset .
    }
    OPTIONAL {
        ?dataset dcat:distribution ?distribution .
        ?distribution a dcat:Distribution ;
                dcat:mediaType ?_mediaType .
    }
} GROUP BY  ?dataset ?description ?startDate ?endDate ?bbox ?used ?parameterName ?catalog LIMIT 10 OFFSET 0